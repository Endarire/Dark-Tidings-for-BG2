IF
	NumTimesTalkedTo(0)
	Detect([PC])
THEN
	RESPONSE #100
		StartDialogNoSet(LastSeenBy(Myself))
END

IF
	See(NearestEnemyOf(Myself))
	Range(LastSeenBy(Myself),10)
	Global("cone_of_cold","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("cone_of_cold","LOCALS",1)
		ForceSpell(LastSeenBy(Myself),WIZARD_CONE_OF_COLD)  // SPWI503.SPL (Kï¿½ltekegel)
END

IF
	See(NearestEnemyOf(Myself))
	Global("shadow_summon","LOCALS",0)
	//LevelPartyGT(9)
	!DifficultyLT(NORMAL)
THEN
	RESPONSE #100
		SetGlobal("shadow_summon","LOCALS",1)
		CreateCreature("AC#DTSH2",[700.986],0)  // Schatten	
		CreateCreature("AC#DTSH2",[614.1105],0)  // Schatten
		CreateCreature("AC#DTSH2",[836.1386],0)  // Schatten
		CreateCreature("AC#DTSH2",[1140.1253],0)  // Schatten
		CreateCreature("AC#DTSH2",[1166.97],0)  // Schatten
		CreateCreature("AC#DTSH2",[583.1396],0)  // Schatten	
		Wait(1)
		Shout(89)
END

IF
	See(ThirdNearest([PC]))
	GlobalLT("invisibility","LOCALS",3)
	HPPercentLT(Myself,75)
	!GlobalTimerNotExpired("invisibility_timer","LOCALS")
THEN
	RESPONSE #100
		ForceSpell(Myself,WIZARD_INVISIBILITY)  // SPWI206.SPL (Unsichtbarkeit)
		IncrementGlobal("invisibility","LOCALS",1)
		SetGlobalTimer("invisibility_timer","LOCALS",THREE_ROUNDS)
		MoveToObject(ThirdNearest([PC]))
END

IF
	See(NearestEnemyOf(Myself))
	!HasBounceEffects(NearestEnemyOf(Myself))
	HPPercentLT(Myself,50)
	Global("finger_of_death","LOCALS",0)
THEN
	RESPONSE #100
		FaceObject(NearestEnemyOf(Myself))
		SetGlobal("finger_of_death","LOCALS",1)
		ForceSpell(NearestEnemyOf(Myself),WIZARD_FINGER_OF_DEATH)  // SPWI713.SPL (Finger des Todes)
END

IF
	OR(2)
		See([0.0.0.CLERIC_ALL])
		See([0.0.0.MAGE_ALL])
	GlobalLT("DispelMagic","LOCALS",3)
	!GlobalTimerNotExpired("dispel_timer","LOCALS")
THEN
	RESPONSE #100
		ForceSpell(LastSeenBy,WIZARD_DISPEL_MAGIC)  // SPWI302.SPL (Magie bannen)
		SetGlobalTimer("dispel_timer","LOCALS",FOUR_ROUNDS)
		IncrementGlobal("DispelMagic","LOCALS",1)
END

IF
	See(NearestEnemyOf(Myself))
	!StateCheck(LastSeenBy(Myself),STATE_STUNNED)
	!StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
	!StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
	!GlobalTimerNotExpired("Confuse","LOCALS")
	GlobalLT("confusion","LOCALS",3)
	!DifficultyLT(NORMAL)
	RandomNum(3,1)
THEN
	RESPONSE #100
		SetGlobalTimer("Confuse","LOCALS",1000)
		IncrementGlobal("confusion","LOCALS",1)
		SpellNoDec(NearestEnemyOf(Myself),WIZARD_CONFUSION)  // SPWI401.SPL (Verwirrung)
END

IF
	See([GOODCUTOFF.0.0.0.0.SUMMONED])
	!StateCheck(LastSeenBy(Myself),STATE_STUNNED)
	!StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
	!StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
	!GlobalTimerNotExpired("Hold_Monster_Timer","LOCALS")
	GlobalLT("HoldMonster","LOCALS",3)
THEN
	RESPONSE #100
		FaceObject(LastSeenBy(Myself))
		SpellNoDec(LastSeenBy(Myself),WIZARD_HOLD_MONSTER)  // SPWI507.SPL (Monster festhalten)
		SetGlobalTimer("Hold_Monster_Timer","LOCALS",TWO_ROUNDS)
		IncrementGlobal("HoldMonster","LOCALS",1)
END

IF
	See(NearestEnemyOf(Myself))
	RandomNum(3,1)
	!GlobalTimerNotExpired("Unholy_Blight","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("Unholy_Blight","LOCALS",TWO_ROUNDS)
		ForceSpell(NearestEnemyOf(Myself),CLERIC_UNHOLY_BLIGHT)  // SPPR314.SPL (Unheiliges Verderben)
END

IF
	See(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("contagion","LOCALS")
	RandomNum(2,1)
THEN
	RESPONSE #100
		SetGlobalTimer("contagion","LOCALS",FIVE_ROUNDS)
		ForceSpell(NearestEnemyOf(Myself),WIZARD_CONTAGION)  // SPWI409.SPL (Ansteckung)
END

IF
	See(NearestEnemyOf(Myself))
	Range(LastSeenBy(Myself),12)
	StateCheck(LastSeenBy,STATE_HELPLESS)
THEN
	RESPONSE #100
		AttackOneRound(NearestEnemyOf(Myself))
		AttackOneRound(NearestEnemyOf(Myself))
		Continue()
END

IF
	See(NearestEnemyOf(Myself))
	Range(LastSeenBy(Myself),10)
THEN
	RESPONSE #100
		AttackReevaluate(NearestEnemyOf(Myself),30)
END

IF
	See(NearestEnemyOf(Myself))
	!Range(LastSeenBy(Myself),10)
	HPPercentLT(Myself,80)
THEN
	RESPONSE #100
		AttackReevaluate(NearestEnemyOf(Myself),30)
END

